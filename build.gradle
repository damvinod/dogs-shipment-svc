plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'

	// static checks
	id 'jacoco'
	id 'com.github.spotbugs' version '6.1.12'

	id 'com.adarshr.test-logger' version '4.0.0'
}

group = 'com.dogs.ship'
version = '1.0'

jar {
	enabled = false
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// SpotBugs configuration
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	testlogger {
		theme 'mocha' // task level
	}
	testLogging.showStandardStreams = true
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn spotbugsMain, spotbugsTest, test
	reports {
		xml.required = true
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
	afterEvaluate {
		classDirectories.setFrom(
			files(classDirectories.files.collect {
				fileTree(dir: it, exclude: ['**/DogsShipmentSvcApplication.class'])
			})
		)
	}
}

tasks.register('downloadDependencies') {
	doLast {
		configurations.compileClasspath.files
		configurations.runtimeClasspath.files
		println 'Dependencies downloaded.'
	}
}

spotbugsMain {
	reports {
		html {
			enabled = true
		}
	}
}

spotbugs {
	excludeFilter = file("./config/spotbugs/excludeFilter.xml")
}

check.dependsOn jacocoTestReport